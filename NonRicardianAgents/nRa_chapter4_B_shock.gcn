# ############################################################################
# Non-Ricardian Agents. Chapter 4, Torres (2015)
# written for gEcon by Dr. Edmundo Molina 
# ############################################################################


options
{
    output logfile = TRUE;
    output LaTeX = TRUE;
};


tryreduce
{
    K_d[], L_d[];
};


block CONSUMER_1
{
    definitions
    {
        u_1[] = gamma*log(C_1[])+(1-gamma)*log(1 - L_1_s[]);
    };
    controls
    {
        K_1_s[], C_1[], L_1_s[], I_1[];
    };
    objective
    {
        U_1[] = u_1[] + B[] * beta * E[][U_1[1]];
    };
    constraints
    {
        I_1[] + C_1[] = r[] * K_1_s[-1] + W[] * L_1_s[];
        K_1_s[] =  I_1[] + (1 - delta) * K_1_s[-1] ;
    };
};


block CONSUMER_2
{
    definitions
    {
        u_2[] = gamma*log(C_2[])+(1-gamma)*log(1 - L_2_s[]);
    };
    controls
    {
        C_2[], L_2_s[];
    };
    objective
    {
        U_2[] = u_2[] + B[] * beta * E[][U_2[1]];
    };
    constraints
    {
         C_2[]= W[] * L_2_s[] ;
     };
    calibration
    {
       delta = 0.06;
       beta = 0.97;
       gamma = 0.40;
    };

};

block AGGREGATION
{
  identities
  {
   C[]=omega*C_1[]+(1-omega)*C_2[];
   L_s[]=omega*L_1_s[]+(1-omega)*L_2_s[];
   K_s[]=omega*K_1_s[] ;
   I[]=omega*I_1[];
  };
  calibration
  {
    omega = 0.50;
  };
};


block FIRM
{
    controls
    {
        K_d[], L_d[], Y[], pi[];
    };
    objective
    {
        PI[] = pi[];
    };
    constraints
    {
        Y[] = A[] * K_d[]^alpha * L_d[]^(1 - alpha);
        pi[] = Y[] - L_d[] * W[] - r[] * K_d[];
    };
    calibration
    {
        alpha = 0.350;
    };
};


block EQUILIBRIUM
{
    identities
    {
     K_d[] = K_s[-1];
     L_d[] = L_s[];
     A[] = 1.0;
    };
};


block EXOG
{
    identities
    {
         B[] = exp(rho_B * log(B[-1]) + epsilon_B[]);
    };
    shocks
    {
        epsilon_B[];
    };
    calibration
    {
        rho_B = 0.95;
    };
};
