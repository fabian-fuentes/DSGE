# ############################################################################
# Taxes Model. Chapter 7, Torres (2015)
# Written by Dr. Edmundo Molina 
# ############################################################################


options
{
    output logfile = TRUE;
    output LaTeX = TRUE;
};


tryreduce
{
    K_d[], L_d[];
};


block CONSUMER
{
    definitions
    {
        u[] = gamma*log(C[])+(1-gamma)*log(1 - L_s[]);
    };
    controls
    {
        K_s[], C[], L_s[], I[];
    };
    objective
    {
        U[] = u[] + beta * E[][U[1]];
    };
    constraints
    {
        I[] + (1+tau_c) * C[] = (1-tau_k) * r[] * K_s[-1] + (1-tau_l) * W[] * L_s[]+ pi[]+ GT[] : lambda_c[] ;
        K_s[] =  I[] + (1 - delta) * K_s[-1] ;
    };
    calibration
    {
        delta = 0.06;
        beta = 0.97;
        gamma = 0.40;
    };
};


block FIRM
{
    controls
    {
        K_d[], L_d[], Y[], pi[];
    };
    objective
    {
        PI[] = pi[];
    };
    constraints
    {
        Y[] = A[] * K_d[]^alpha * L_d[]^(1 - alpha);
        pi[] = Y[] - L_d[] * W[] - r[] * K_d[];
    };
    calibration
    {
        alpha = 0.350;
    };
};

block GOVERNMENT
{
    identities
    {
        GT[] = G_inc[] ;
        G_inc[] = T_c[] + T_l[] + T_k[];
        T_c[] = tau_c * C[];
        T_l[] = tau_l  * W[] * L_s[] ;
        T_k[] = tau_k * (r[] - delta ) * K_s[-1];
    };
    calibration
    {
        tau_l = 0.348;
        tau_c = 0.116;
        tau_k = 0.225;
    };
};

block EQUILIBRIUM
{
    identities
    {
        K_d[] = K_s[-1];
        L_d[] = L_s[];
    };
};


block EXOG
{
    identities
    {
        A[] = exp(rho * log(A[-1]) + epsilon_A[]);
    };
    shocks
    {
        epsilon_A[];
    };
    calibration
    {
        rho = 0.95;
    };
};
