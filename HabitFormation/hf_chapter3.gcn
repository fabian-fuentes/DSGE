# ############################################################################
# Habit Formation Model. Chapter 3, Torres (2015)
# Dr. Edmundo Molina 
# ############################################################################


options
{
    output logfile = TRUE;
    output LaTeX = TRUE;
};


tryreduce
{
    K_d[], L_d[];
};


block CONSUMER
{
    definitions
    {
        u[] = gamma*log(C[]-phi*C[-1])+(1-gamma)*log(1 - L_s[]);
    };
    controls
    {
        K_s[], C[], L_s[], I[];
    };
    objective
    {
        U[] = u[] + beta * E[][U[1]];
    };
    constraints
    {
        I[] + C[] = r[] * K_s[-1] + W[] * L_s[]+ pi[] ;
        K_s[] =  I[] + (1 - delta) * K_s[-1] ;
    };
    calibration
    {
        delta = 0.06;
        beta = 0.97;
        gamma = 0.40;
        phi= 0.80;
    };
};


block FIRM
{
    controls
    {
        K_d[], L_d[], Y[], pi[];
    };
    objective
    {
        PI[] = pi[];
    };
    constraints
    {
        Y[] = A[] * K_d[]^alpha * L_d[]^(1 - alpha);
        pi[] = Y[] - L_d[] * W[] - r[] * K_d[];
    };
    calibration
    {
        alpha = 0.350;
    };
};


block EQUILIBRIUM
{
    identities
    {
        K_d[] = K_s[-1];
        L_d[] = L_s[];
    };
};


block EXOG
{
    identities
    {
        A[] = exp(rho * log(A[-1]) + epsilon_A[]);
    };
    shocks
    {
        epsilon_A[];
    };
    calibration
    {
        rho = 0.95;
    };
};
